// SPDX-FileCopyrightText: 2025 Lukáš Lalinský
// SPDX-License-Identifier: MIT

const std = @import("std");
const builtin = @import("builtin");
const log = @import("common.zig").log;
const meta = @import("meta.zig");
const Runtime = @import("runtime.zig").Runtime;
const getCurrentExecutor = @import("runtime.zig").getCurrentExecutor;
const beginShield = @import("runtime.zig").beginShield;
const endShield = @import("runtime.zig").endShield;
const sleep = @import("runtime.zig").sleep;
const JoinHandle = @import("runtime.zig").JoinHandle;
const WaitQueue = @import("utils/wait_queue.zig").WaitQueue;
const Awaitable = @import("awaitable.zig").Awaitable;
const spawnTask = @import("task.zig").spawnTask;
const spawnBlockingTask = @import("blocking_task.zig").spawnBlockingTask;
const Futex = @import("sync/Futex.zig");

pub const Group = struct {
    inner: std.Io.Group = .init,

    pub const init: Group = .{};

    /// Reinterpret a `*std.Io.Group` as `*Group`. The two share layout via
    /// `IoGroup`, so a pointer cast is sufficient.
    pub fn fromStd(g: *std.Io.Group) *Group {
        return @ptrCast(@alignCast(g));
    }

    // Interpret inner.token as WaitQueue head
    //   null (0)  = sentinel0 = idle/done
    //   1         = sentinel1 = closing (reject new spawns)
    //   pointer   = has tasks

    // Interpret inner.state's lower u32 as combined counter+flags
    // Layout: [8 bits flags (high)][24 bits counter (low)]
    // This allows futex to observe both counter and flags simultaneously,
    // enabling features like fail_fast to wake waiters immediately.
    // 24-bit counter supports up to 16,777,215 concurrent tasks.
    const counter_mask: u32 = 0x00FFFFFF;
    const flags_shift: u5 = 24;

    // Flag bits in upper 8 bits
    const canceled_bit: u32 = 1 << 24;
    const failed_bit: u32 = 1 << 25;
    const fail_fast_bit: u32 = 1 << 26;
    const closed_bit: u32 = 1 << 27;

    fn getTasks(self: *Group) *WaitQueue(GroupNode) {
        return @ptrCast(&self.inner.token);
    }

    fn getState(self: *Group) *u32 {
        // Cast usize* to u32* - identity on 32-bit, gets lower u32 on 64-bit little-endian
        return @ptrCast(@alignCast(&self.inner.state));
    }

    /// Set the failed flag. If fail_fast is set, also closes the group.
    /// TODO: Wake waiters immediately when this bit transitions from unset to set.
    /// Currently waiters only wake when counter reaches zero.
    pub fn setFailed(self: *Group) void {
        const state_ptr = self.getState();
        var state = @atomicLoad(u32, state_ptr, .acquire);
        while (true) {
            var new_state = state | failed_bit;
            if (state & fail_fast_bit != 0) new_state |= closed_bit;
            if (new_state == state) return;
            state = @cmpxchgWeak(u32, state_ptr, state, new_state, .acq_rel, .acquire) orelse return;
        }
    }

    /// Check if the failed flag is set.
    pub fn hasFailed(self: *Group) bool {
        return (@atomicLoad(u32, self.getState(), .acquire) & failed_bit) != 0;
    }

    /// Set the canceled flag. If fail_fast is set, also closes the group.
    /// TODO: Wake waiters immediately when this bit transitions from unset to set.
    /// Currently waiters only wake when counter reaches zero.
    pub fn setCanceled(self: *Group) void {
        const state_ptr = self.getState();
        var state = @atomicLoad(u32, state_ptr, .acquire);
        while (true) {
            var new_state = state | canceled_bit;
            if (state & fail_fast_bit != 0) new_state |= closed_bit;
            if (new_state == state) return;
            state = @cmpxchgWeak(u32, state_ptr, state, new_state, .acq_rel, .acquire) orelse return;
        }
    }

    /// Check if the canceled flag is set.
    pub fn isCanceled(self: *Group) bool {
        return (@atomicLoad(u32, self.getState(), .acquire) & canceled_bit) != 0;
    }

    /// Set the fail_fast flag.
    /// TODO: Implement early wake on first error/cancel when fail_fast is set.
    /// The unified counter+flags state makes this feasible - setFailed/setCanceled
    /// just need to check fail_fast and wake waiters when the bit transitions.
    pub fn setFailFast(self: *Group) void {
        _ = @atomicRmw(u32, self.getState(), .Or, fail_fast_bit, .acq_rel);
    }

    /// Check if the fail_fast flag is set.
    pub fn isFailFast(self: *Group) bool {
        return (@atomicLoad(u32, self.getState(), .acquire) & fail_fast_bit) != 0;
    }

    fn setClosed(self: *Group) void {
        _ = @atomicRmw(u32, self.getState(), .Or, closed_bit, .acq_rel);
    }

    fn isClosed(self: *Group) bool {
        return (@atomicLoad(u32, self.getState(), .acquire) & closed_bit) != 0;
    }

    pub fn spawn(self: *Group, func: anytype, args: std.meta.ArgsTuple(@TypeOf(func))) !void {
        const rt = getCurrentExecutor().runtime;
        const Args = @TypeOf(args);
        const ReturnType = @typeInfo(@TypeOf(func)).@"fn".return_type.?;
        const Context = struct { group: *Group, args: Args };
        const Wrapper = struct {
            fn start(ctx: *const anyopaque) void {
                const context: *const Context = @ptrCast(@alignCast(ctx));
                const group = context.group;
                if (@typeInfo(ReturnType) == .error_union) {
                    @call(.auto, func, context.args) catch |err| {
                        if (err == error.Canceled) {
                            group.setCanceled();
                        } else {
                            log.err("Group task failed with error: {}", .{err});
                            group.setFailed();
                        }
                    };
                } else {
                    _ = @call(.auto, func, context.args);
                }
            }
        };

        const context: Context = .{ .group = self, .args = args };
        return groupSpawnTask(self, rt, std.mem.asBytes(&context), .fromByteUnits(@alignOf(Context)), &Wrapper.start);
    }

    pub fn spawnBlocking(self: *Group, func: anytype, args: std.meta.ArgsTuple(@TypeOf(func))) !void {
        const rt = getCurrentExecutor().runtime;
        const Args = @TypeOf(args);
        const ReturnType = @typeInfo(@TypeOf(func)).@"fn".return_type.?;
        const Context = struct { group: *Group, args: Args };
        const Wrapper = struct {
            fn start(ctx: *const anyopaque, _: *anyopaque) void {
                const context: *const Context = @ptrCast(@alignCast(ctx));
                const group = context.group;
                if (@typeInfo(ReturnType) == .error_union) {
                    @call(.auto, func, context.args) catch |err| {
                        if (err == error.Canceled) {
                            group.setCanceled();
                        } else {
                            log.err("Group blocking task failed with error: {}", .{err});
                            group.setFailed();
                        }
                    };
                } else {
                    _ = @call(.auto, func, context.args);
                }
            }
        };

        const context: Context = .{ .group = self, .args = args };
        return groupSpawnBlockingTask(self, rt, std.mem.asBytes(&context), .fromByteUnits(@alignOf(Context)), &Wrapper.start);
    }

    pub fn wait(group: *Group) Cancelable!void {
        group.setClosed();
        errdefer group.cancel();

        // Wait for all tasks to complete
        const state_ptr = group.getState();
        while (true) {
            const state = @atomicLoad(u32, state_ptr, .acquire);
            const counter = state & counter_mask;
            if (counter == 0) break;
            try Futex.wait(state_ptr, state);
        }

        // All tasks completed - verify list is empty
        // Tasks remove themselves in onGroupTaskComplete
        std.debug.assert(!group.getTasks().hasWaiters());
    }

    pub fn cancel(group: *Group) void {
        beginShield();
        defer endShield();

        group.setCanceled();

        // Pop all tasks to cancel them while setting the "canceled" flag.
        // Note: the group typically lives on the caller's stack so cancel()/release()
        // don't free `self`, but we still break on is_last for consistency.
        while (group.getTasks().popAndSetFlag()) |result| {
            const awaitable: *Awaitable = @fieldParentPtr("group_node", result.node);
            awaitable.cancel();
            awaitable.release();
            if (result.is_last) break;
        }

        // Wait for all tasks to complete
        const state_ptr = group.getState();
        while (true) {
            const state = @atomicLoad(u32, state_ptr, .acquire);
            const counter = state & counter_mask;
            if (counter == 0) break;
            Futex.wait(state_ptr, state) catch unreachable;
        }

        // Clear the canceled flag for reuse
        group.getTasks().clearFlag();
    }
};

/// Spawn a task in the group with raw context bytes and start function.
/// Used by Group.spawn and std.Io vtable implementations.
pub fn groupSpawnTask(
    group: *Group,
    rt: *Runtime,
    context: []const u8,
    context_alignment: std.mem.Alignment,
    start: *const fn (context: *const anyopaque) void,
) !void {
    _ = try spawnTask(rt, 0, .@"1", context, context_alignment, .{ .group = start }, group);
}

/// Spawn a blocking task in the group with raw context bytes and start function.
/// Used by Group.spawnBlocking.
pub fn groupSpawnBlockingTask(
    group: *Group,
    rt: *Runtime,
    context: []const u8,
    context_alignment: std.mem.Alignment,
    start: *const fn (context: *const anyopaque, result: *anyopaque) void,
) !void {
    _ = try spawnBlockingTask(rt, 0, .@"1", context, context_alignment, .{ .regular = start }, group);
}

/// Register an awaitable with a group.
/// Increments counter, sets group_node.group, and adds to task list.
/// Returns error.Closed if group is closed.
pub fn registerGroupTask(group: *Group, awaitable: *Awaitable) error{Closed}!void {
    if (group.isClosed()) return error.Closed;
    const prev_state = @atomicRmw(u32, group.getState(), .Add, 1, .acq_rel);
    const prev_counter = prev_state & Group.counter_mask;
    std.debug.assert(prev_counter < Group.counter_mask); // Check for overflow
    awaitable.group_node.group = group;
    // Push unless canceled (flag set means group was canceled)
    if (!group.getTasks().pushUnlessFlag(&awaitable.group_node)) {
        const state_ptr = group.getState();
        const state_before_sub = @atomicRmw(u32, state_ptr, .Sub, 1, .acq_rel);
        if (state_before_sub & Group.counter_mask == 1) {
            Futex.wake(state_ptr, std.math.maxInt(u32));
        }
        return error.Closed;
    }
}

/// Unregister an awaitable from a group.
/// Removes from task list, releases ref, decrements counter, and wakes waiters if last task.
pub fn unregisterGroupTask(group: *Group, awaitable: *Awaitable) void {
    // Only release if we successfully removed it (cancel might have popped it first)
    if (group.getTasks().remove(&awaitable.group_node)) {
        awaitable.release();
    }

    const state_ptr = group.getState();
    const prev_state = @atomicRmw(u32, state_ptr, .Sub, 1, .acq_rel);
    const prev_counter = prev_state & Group.counter_mask;
    if (prev_counter == 1) {
        Futex.wake(state_ptr, std.math.maxInt(u32));
    }
}

pub const GroupNode = struct {
    group: ?*Group = null,

    next: ?*GroupNode = null,
    prev: ?*GroupNode = null,
    in_list: if (std.debug.runtime_safety) bool else void = if (std.debug.runtime_safety) false else {},

    userdata: usize = undefined,
};

const Cancelable = @import("common.zig").Cancelable;

fn testFn(arg: usize) usize {
    return arg + 1;
}

test "Group: spawn" {
    const rt = try Runtime.init(std.testing.allocator, .{});
    defer rt.deinit();

    var group: Group = .init;
    defer group.cancel();

    try group.spawn(testFn, .{0});

    try group.wait();
}

test "Group: wait for multiple tasks" {
    const rt = try Runtime.init(std.testing.allocator, .{});
    defer rt.deinit();

    const completed = struct {
        var value: usize = 0;

        fn task() void {
            _ = @atomicRmw(usize, &value, .Add, 1, .monotonic);
        }
    };

    completed.value = 0;

    var group: Group = .init;
    defer group.cancel();

    try group.spawn(completed.task, .{});
    try group.spawn(completed.task, .{});
    try group.spawn(completed.task, .{});

    try group.wait();

    try std.testing.expectEqual(3, completed.value);
}

test "Group: cancellation while waiting" {
    const rt = try Runtime.init(std.testing.allocator, .{});
    defer rt.deinit();

    const counts = struct {
        var started: usize = 0;
        var canceled: usize = 0;

        fn slowTask() void {
            _ = @atomicRmw(usize, &started, .Add, 1, .monotonic);
            sleep(.fromMilliseconds(1000)) catch {
                _ = @atomicRmw(usize, &canceled, .Add, 1, .monotonic);
            };
        }
    };

    const cancellerTask = struct {
        fn call(runtime: *Runtime, group_handle: *JoinHandle(anyerror!void)) !void {
            // Wait a bit for tasks to start
            try runtime.sleep(.fromMilliseconds(10));
            // Cancel the group waiter
            group_handle.awaitable.?.cancel();
        }
    }.call;

    const groupTask = struct {
        fn call() anyerror!void {
            var group: Group = .init;
            defer group.cancel();

            // Spawn multiple slow tasks
            try group.spawn(counts.slowTask, .{});
            try group.spawn(counts.slowTask, .{});
            try group.spawn(counts.slowTask, .{});

            // This wait should be interrupted by cancellation
            group.wait() catch {};
        }
    }.call;

    counts.started = 0;
    counts.canceled = 0;

    // Spawn the group task
    var group_handle = try rt.spawn(groupTask, .{});

    // Spawn a task that will cancel the group task
    var canceller = try rt.spawn(cancellerTask, .{ rt, &group_handle });
    defer canceller.cancel();

    // Wait for group task to complete (should be canceled)
    try group_handle.join();

    // All tasks should have been canceled
    try std.testing.expectEqual(3, counts.started);
    try std.testing.expectEqual(3, counts.canceled);
}

test "Group: failed task does not close group" {
    const rt = try Runtime.init(std.testing.allocator, .{});
    defer rt.deinit();

    const T = struct {
        var counter: usize = 0;

        fn task(i: usize) !void {
            if (i % 2 == 0) {
                _ = @atomicRmw(usize, &counter, .Add, 1, .monotonic);
            } else {
                return error.OddNumber;
            }
        }
    };

    T.counter = 0;

    var group: Group = .init;
    defer group.cancel();

    const n = 10;
    for (0..n) |i| {
        try group.spawn(T.task, .{i});
        try sleep(.fromMilliseconds(1));
    }

    try group.wait();

    try std.testing.expectEqual(n / 2, T.counter);
}
